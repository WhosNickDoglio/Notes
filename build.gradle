buildscript {
    apply from: 'dependencies.gradle'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.4'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.0.0"
        classpath 'com.jaredsburrows:gradle-license-plugin:0.8.42'
    }
}

plugins {
    id('com.gradle.build-scan') version('2.3')
    id("io.gitlab.arturbosch.detekt") version("1.0.0-RC14")
}

buildScan {
    setTermsOfServiceUrl("https://gradle.com/terms-of-service")
    setTermsOfServiceAgree("yes")
    publishAlways()
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += [
                "-Adagger.formatGeneratedSource=disabled",
                "-Adagger.gradle.incremental=true"
        ]
    }

    afterEvaluate {
        if (project.plugins.hasPlugin("kotlin-kapt") || //Not sure if both are needed or just one?
                project.plugins.hasPlugin("org.jetbrains.kotlin.kapt")) {
            kapt {
                useBuildCache = true
                arguments {
                    arg("dagger.formatGeneratedSource", "disabled")
                    arg("dagger.gradle.incremental", "enabled")
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
