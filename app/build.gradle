apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco-android'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.nicholasdoglio.notes"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        signingConfigs {
            config {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    anko = "0.10.4"
    support = "27.0.2"
    constraint = "1.1.0-beta4"
    dagger = "2.14.1"
    paging = "1.0.0-alpha4-1"
    archComponents = "1.0.0"
    rxJava = "2.1.7"
    rxAndroid = "2.0.1"
    rxKotlin = "2.2.0"
    rxBinding = "2.0.0"
    dispose = "0.5.1"
    junit = "4.12"
    robo = "3.6.1"
    runner = "1.0.1"
    espresso = "3.0.1"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    //Android
    implementation "com.android.support:appcompat-v7:$support"
    implementation "com.android.support:recyclerview-v7:$support"
    implementation "com.android.support:design:$support"
    implementation "com.android.support:support-v4:$support"
    implementation "com.android.support.constraint:constraint-layout:$constraint"

    //Anko
    implementation "org.jetbrains.anko:anko-appcompat-v7-commons:$anko"

    //Android Architecture Components
    implementation "android.arch.paging:runtime:$paging"
    implementation "android.arch.lifecycle:common-java8:$archComponents"
    implementation "android.arch.lifecycle:extensions:$archComponents"
    implementation "android.arch.persistence.room:runtime:$archComponents"
    implementation "android.arch.persistence.room:rxjava2:$archComponents"
    implementation 'com.android.support:support-v4:27.0.2'
    kapt "android.arch.persistence.room:compiler:$archComponents"

    //Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBinding"

    //AutoDispose
    implementation "com.uber.autodispose:autodispose-kotlin:$dispose"
    implementation "com.uber.autodispose:autodispose-android-kotlin:$dispose"
    implementation "com.uber.autodispose:autodispose-android-archcomponents-kotlin:$dispose"

    //Dagger
    implementation "com.google.dagger:dagger:$dagger"
    implementation "com.google.dagger:dagger-android:$dagger"
    implementation "com.google.dagger:dagger-android-support:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"

    //Testing
    testImplementation "junit:junit:$junit"
    testImplementation "android.arch.persistence.room:testing:$archComponents"
    testImplementation "android.arch.core:core-testing:$archComponents"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.robolectric:robolectric:$robo"
    androidTestImplementation "com.android.support.test:runner:$runner"
}