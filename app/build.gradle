apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'

//Need to make this work with Travis
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.nicholasdoglio.notes"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 3
        versionName "1.0.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        signingConfigs {
            config {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}

ext {
    anko = "0.10.4"
    support = "27.0.2"
    constraint = "1.1.0-beta4"
    dagger = "2.14.1"
    paging = "1.0.0-alpha5"
    archComponents = "1.1.0"
    room = "1.1.0-alpha1"
    rxJava = "2.1.9"
    rxAndroid = "2.0.1"
    rxKotlin = "2.2.0"
    rxBinding = "2.0.0"
    rxRelay = "2.0.0"
    dispose = "0.5.1"
    junit = "4.12"
    robo = "3.6.1"
    runner = "1.0.1"
    espresso = "3.0.1"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    //Android
    implementation "com.android.support:appcompat-v7:$support"
    implementation "com.android.support:recyclerview-v7:$support"
    implementation "com.android.support:design:$support"
    implementation "com.android.support:support-v4:$support"
    implementation "com.android.support.constraint:constraint-layout:$constraint"

    //Anko
    implementation "org.jetbrains.anko:anko-appcompat-v7-commons:$anko"

    //Android Architecture Components
    implementation "android.arch.paging:runtime:$paging"
    implementation "android.arch.lifecycle:common-java8:$archComponents"
    implementation "android.arch.lifecycle:extensions:$archComponents"
    implementation "android.arch.persistence.room:runtime:$room"
    implementation "android.arch.persistence.room:rxjava2:$room"
    kapt "android.arch.persistence.room:compiler:$room"

    //Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBinding"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rxRelay"

    //AutoDispose
    implementation "com.uber.autodispose:autodispose-kotlin:$dispose"
    implementation "com.uber.autodispose:autodispose-android-kotlin:$dispose"
    implementation "com.uber.autodispose:autodispose-android-archcomponents-kotlin:$dispose"

    //Dagger
    implementation "com.google.dagger:dagger:$dagger"
    implementation "com.google.dagger:dagger-android:$dagger"
    implementation "com.google.dagger:dagger-android-support:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"

    //Testing
    testImplementation "junit:junit:$junit"
    testImplementation "android.arch.persistence.room:testing:$room"
    testImplementation "android.arch.core:core-testing:$archComponents"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.robolectric:robolectric:$robo"
    androidTestImplementation "com.android.support.test:runner:$runner"
}

jacoco {
    toolVersion = "0.8.0"
    reportsDir = file("$buildDir/reports")
}

//This is still needs work
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/test/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree], [kotlinDebugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec"
    ])
}
